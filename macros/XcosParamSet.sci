function XcosParamSet(SetType,Number)
    global bOK;
    global AutoGenHelpToolbox_root;
    bOK = %f;
    
    if (SetType == "Input") then
        ParamTypeContext = "输入";
    elseif (SetType == "Output") then
        ParamTypeContext = "输出";
    elseif (SetType == "Params") then
        ParamTypeContext = "参数";
    end
    screen_area = get(0, "screensize_px")
    XcosParamUI_wight = 260;
    XcosParamUI_high = 280;
    XcosParamUI_x = (screen_area(3)-XcosParamUI_wight)/2;
    XcosParamUI_y = (screen_area(4)-XcosParamUI_high)/2;
    //R = 255;G = 233; B = 220;
    R = 28;G = 120; B = 135;
    XcosParamUI = figure("figure_name", "第" + string(Number) + "个" + ParamTypeContext +"属性设置",...
    "infobar_visible", "off",...
    "toolbar_visible", "off",...
    "dockable", "off",...
    "menubar", "none",...
    "default_axes", "off", ...
    "Position",[XcosParamUI_x XcosParamUI_y XcosParamUI_wight XcosParamUI_high],...
    "resize", "off",...
    "BackgroundColor", [R/255 G/255 B/255],...
    "Icon",AutoGenHelpToolbox_root+"images"+filesep()+"Example_images.png",...
    "immediate_drawing","on",...
    "Tag", "XcosParamSet",...
    "visible", "off");
    //参数名称标题
    ParamName_title = uicontrol(XcosParamUI,"Position", [25 240 80 25], ...//2
    "Style", "text",...
    "FontSize", 18,...
    "String", "参数名称",...
    "Verticalalignment","middle",...
    "ForegroundColor", [255/255 255/255 255/255],...
    "BackgroundColor", [R/255 G/255 B/255]);
    //参数名称文本框
    ParamName_Context = uicontrol(XcosParamUI, "Position", [105 240 130 25],...//3
    "Border",[],...
    "Style", "edit",...
    "String", "",...
    "Callback_Type",10,...
    "Verticalalignment","top",...
    "Tag", "ParamNameEdit_Tag");
    //数据大小设定
    ParamSize_title = uicontrol(XcosParamUI,"Position", [25 210 80 25], ...//2
    "Style", "text",...
    "FontSize", 18,...
    "String", "数据大小",...
    "Verticalalignment","middle",...
    "ForegroundColor", [255/255 255/255 255/255],...
    "BackgroundColor", [R/255 G/255 B/255]);
    //数据大小文本框1
    ParamSize_Context1 = uicontrol(XcosParamUI, "Position", [105 210 50 25],...//3
    "Border",[],...
    "Style", "edit",...
    "String", "",...
    "Callback_Type",10,...
    "Verticalalignment","top",...
    "Tag", "ParamSizeEdit1_Tag");
    //数据大小乘号
    ParamSize_title = uicontrol(XcosParamUI,"Position", [160 210 20 25], ...//2
    "Style", "text",...
    "FontSize", 18,...
    "String", "×",...
    "Verticalalignment","middle",...
    "ForegroundColor", [255/255 255/255 255/255],...
    "BackgroundColor", [R/255 G/255 B/255]);
    //数据大小文本框2
    ParamSize_Context2 = uicontrol(XcosParamUI, "Position", [185 210 50 25],...//3
    "Border",[],...
    "Style", "edit",...
    "String", "",...
    "Callback_Type",10,...
    "Verticalalignment","top",...
    "Tag", "ParamSizeEdit2_Tag");
    //数据类型设定
    ParamType_title = uicontrol(XcosParamUI,"Position", [25 180 80 25], ...//2
    "Style", "text",...
    "FontSize", 18,...
    "String", "参数类型",...
    "Verticalalignment","middle",...
    "ForegroundColor", [255/255 255/255 255/255],...
    "BackgroundColor", [R/255 G/255 B/255]);
    //参数类型文本框
    ParamType_Context = uicontrol(XcosParamUI, "Position", [105 180 130 25],...//3
    "Style", "popupmenu",...
    "String", "real|complex|double|string|int32|int16|int8|uint32|uint16|uint8",...
    "Value",1,...
    "Callback_Type",10,...
    "Tag", "ParamTypePop_Tag");
    //详细描述设定
    ParamDetail_title = uicontrol(XcosParamUI,"Position", [25 150 80 25], ...//2
    "Style", "text",...
    "FontSize", 18,...
    "String", "详细描述",...
    "Verticalalignment","middle",...
    "ForegroundColor", [255/255 255/255 255/255],...
    "BackgroundColor", [R/255 G/255 B/255]);
    //详细描述文本框
    ParamDetail_Context = uicontrol(XcosParamUI, "Position", [105 97 130 75],...//3
    "Style", "edit",...
    "String", "",...
    "Max",2,...
    "Value",1,...
    "Callback_Type",10,...
    "Verticalalignment","top",...
    "Tag", "ParamDetailEdit_Tag");
    //确定按钮
    ok_button = uicontrol(XcosParamUI, "Position", [145 25 80 30], ...//10
    "Style", "pushbutton",...
    "FontSize", 16,...
    "String", "下一步", ...
    "Callback_Type",10,...
    "callback", "XcosParamUIok_action()");
    //取消按钮
    cancel_button = uicontrol(XcosParamUI, "Position", [40 25 80 30], ...//11
    "Style", "pushbutton",...
    "FontSize", 16,...
    "String", "取  消", ...
    "Callback_Type",10,...
    "callback", "XcosParamUIcancel_action()");
//显示界面
    XcosParamUI.visible = "on";
//卡循环等待选择结束
    global Stop;
    Stop = %f;
    while %t,
        if  XcosParamUI_quit() && Stop then
            break;
        end
    end
    clearglobal Stop;
endfunction

//关闭按钮的执行函数
function [re] = XcosParamUI_quit()
    re = %f;
    if (findobj("Tag","XcosParamSet") == []) then
        re = %t;
        global Stop;
        Stop = %t;
    end
endfunction
//

//检查参数函数
function [res] = checkParamFromParamSet()
    res = %f;
    HandleName = ["ParamNameEdit_Tag","ParamSizeEdit1_Tag","ParamSizeEdit2_Tag","ParamDetailEdit_Tag"];
    WarningMessage = ["参数名称","参数大小维度一","参数大小维度二","参数详细说明"]
    for i = 1:size(HandleName,2)
        execstr("handle = findobj(""Tag"","""+HandleName(i)+""");");
        if handle.String == "" then
            messagebox(WarningMessage(i)+"不能为空！", "警告！", "warning");
            return;
        end
    end
    res = %t;
endfunction
//下一步按钮回调函数
function XcosParamUIok_action()
    res = checkParamFromParamSet();
    if (res) then
        GetValueFromParamSetUI();
        global bOK;
        bOK = %t;
        handle = findobj("Tag","XcosParamSet");
        close(handle);
    end
endfunction
//取消按钮回调函数
function XcosParamUIcancel_action()
    global bOK;
    global xcosobj;
    xcosobj = [];
    bOK = %f;
    handle = findobj("Tag","XcosParamSet");
    close(handle);
endfunction
//从界面获取填入的数据
function GetValueFromParamSetUI()
    global xcosobj;
    typestruct = "xcosobj.ParamsData";
    HandleName = ["ParamNameEdit_Tag","ParamSizeEdit1_Tag","ParamSizeEdit2_Tag","ParamDetailEdit_Tag"];
    StructName = ["Name","Dim1","Dim2","Details"];
    
    for i = 1:size(HandleName,2)
        execstr("handle = findobj(""Tag"","""+HandleName(i)+""");");
        execstr(typestruct+"."+StructName(i)+"=["+typestruct+"."+StructName(i)+",handle.String];");
    end
    HandleName = ["ParamTypePop_Tag"];
    StructName = ["Type"];
    for i = 1:size(HandleName,2)
        execstr("handle = findobj(""Tag"","""+HandleName(i)+""");");
        execstr(typestruct+"."+StructName(i)+"=["+typestruct+"."+StructName(i)+",handle.String(handle.Value)];");
    end
endfunction

